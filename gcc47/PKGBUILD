pkgname=gcc47
pkgver=4.7.3
pkgrel=1
pkgdesc="GNU C Compiler"
arch=('i686' 'x86_64')
depends=('libgmp' 'libmpfr' 'libmpc' 'zlib' 'binutils>=2.24')
provides=('gcc')
groups=("base-dev")
url="http://www.gnu.org/software/gcc/"
license=('GPL')
source=("http://ftp.gnu.org/gnu/gcc/gcc-$pkgver/gcc-$pkgver.tar.bz2")
md5sums=('SKIP')

prepare() {
  cd "gcc-$pkgver"
  
  sed -i 's/linux-gnu\* |/linux-gnu* | linux-musl* |/' config.sub
  sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in
  sed -i 's@/lib/ld-linux.so.2@/lib/ld-musl-i386.so.1@' gcc/config/i386/linux.h
  sed -i 's@/lib/ld-linux.so.2@/lib/ld-musl-i386.so.1@' gcc/config/i386/linux64.h
  sed -i 's@/lib64/ld-linux-x86-64.so.2@/lib/ld-musl-x86_64.so.1@' gcc/config/i386/linux64.h

  if [ -f "./config.cache" ]; then
    rm config.cache
  fi
}

build() {
  cd "gcc-$pkgver"
  LDFLAGS="-Wl,--no-keep-memory" ./configure -C \
      --with-headers=no --prefix=/ --disable-shared \
      --disable-multilib --disable-nls --disable-mudflap --disable-libmudflap \
      --disable-libssp --disable-libgomp --disable-debug --disable-bootstrap \
      --enable-lto --with-system-zlib --with-target-libiberty=no --with-target-zlib=no \
      --enable-languages=c --enable-clocale=generic --build=$CARCH-unknown-linux-musl \
      --libdir=/lib --libexecdir=/lib --mandir=/share/man --infodir=/share/info

  make
}

package() {
  cd "gcc-$pkgver"
  make DESTDIR="${pkgdir}" install-gcc install-lto-plugin install-target-libgcc
  ln -sf gcc "${pkgdir}"/bin/cc
}
