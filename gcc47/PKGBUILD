pkgname=("gcc47" "libstdc++")
pkgver=4.7.4
pkgrel=3
pkgdesc="GNU C Compiler"
arch=('i686' 'x86_64')
groups=("base-dev")
url="http://www.gnu.org/software/gcc/"
license=('GPL')
source=("http://ftp.gnu.org/gnu/gcc/gcc-$pkgver/gcc-$pkgver.tar.bz2" "os_defines.h")
md5sums=('SKIP' 'SKIP')

prepare() {
  cd "gcc-$pkgver"
  
  # gcc fixes
  sed -i 's/linux-gnu\* |/linux-gnu* | linux-musl* |/' config.sub
  sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in
  sed -i 's@/lib/ld-linux.so.2@/lib/ld-musl-i386.so.1@' gcc/config/i386/linux.h
  sed -i 's@/lib/ld-linux.so.2@/lib/ld-musl-i386.so.1@' gcc/config/i386/linux64.h
  sed -i 's@/lib64/ld-linux-x86-64.so.2@/lib/ld-musl-x86_64.so.1@' gcc/config/i386/linux64.h

  # g++ fixes
  sed -i 's,gcc_no_link=yes,gcc_no_link=no,' ./libstdc++-v3/configure
  mv ./libstdc\+\+-v3/config/os/gnu-linux ./libstdc\+\+-v3/config/os/gnu-linux.org
  cp -r ./libstdc\+\+-v3/config/os/generic ./libstdc\+\+-v3/config/os/gnu-linux

  cp ./libstdc++-v3/config/os/gnu-linux.org/arm-eabi-extra.ver ./libstdc++-v3/config/os/gnu-linux/
  cp ../os_defines.h libstdc++-v3/config/os/gnu-linux/

  # fixes for the removal of /usr
  for i in gcc/Makefile.in fixincludes/fixinc.in gcc/configure libcpp/configure \
    intl/configure libstdc++-v3/configure libjava/classpath/configure \
    libjava/configure gcc/config.gcc; do
    echo "patching $i (/usr -> /)"
    sed -i "s/\/usr\//\//g" $i
    sed -i "s/\/usr/\//g" $i
  done

  # misc fixes
  mv ./libitm/config/linux/x86 ./libitm/config/linux/x86_glibc
  cp -r ./libitm/config/generic ./libitm/config/linux/x86

  if [ -f "./config.cache" ]; then
    rm config.cache
  fi
}

build() {
  cd "gcc-$pkgver"
  LDFLAGS="-Wl,--no-keep-memory" ./configure -C \
      --with-headers=no --prefix=/ --enable-shared=libstdc++ \
      --disable-multilib --disable-nls --disable-mudflap --disable-libmudflap \
      --disable-libssp --disable-libgomp --disable-debug --disable-bootstrap \
      --enable-lto --with-system-zlib --with-target-libiberty=no --with-target-zlib=no \
      --enable-languages=c,c++ --enable-clocale=generic \
      --libdir=/lib --libexecdir=/lib --mandir=/share/man --infodir=/share/info --oldincludedir=/include

  make
}

package_gcc47() {
  depends=('libgmp' 'libmpfr' 'libmpc' 'zlib' 'binutils>=2.24' 'libstdc++')
  provides=("gcc" "g++")
  cd "gcc-$pkgver"
  make DESTDIR="${pkgdir}" install-gcc
  make DESTDIR="${pkgdir}" install-lto-plugin
  make DESTDIR="${pkgdir}" install-target-libgcc
  make DESTDIR="${pkgdir}" install-target-libstdc++-v3
  ln -sf gcc "${pkgdir}"/bin/cc

  rm $pkgdir/lib/gcc/*/$pkgver/include/stddef.h
  rm -r $pkgdir/lib64
}

package_libstdc++() {
  cd "gcc-$pkgver"
  make DESTDIR="${pkgdir}" install-target-libstdc++-v3

  cd $pkgdir
  if [ -d "lib64" ]; then
    mkdir -p lib
    cp -Rpv lib64/* lib/
    rm -r lib64
  fi
  sed -i -e "s/lib64/lib/g" -e "s/lib32/lib/g" lib/*.la
  rm lib/*.a
  rm -r include share
}
