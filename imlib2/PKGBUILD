pkgname=("imlib2" "imlib2-tools")
pkgver=1.4.6
pkgrel=2
pkgdesc="image loading library"
arch=('i686' 'x86_64')
source=("http://downloads.sourceforge.net/enlightenment/$pkgname-$pkgver.tar.bz2"
        "imlib2-bin-tinyxlib.patch"
        "imlib2-giflib-5.1.0.patch")
md5sums=('SKIP' 'SKIP' 'SKIP')
options=('strip' 'debug')
depends=("freetype" "libjpeg" "libpng" "libgif")
makedepends=("zlib" "bzip2" "tinyxlib")

prepare() {
  cd "imlib2-$pkgver"
  patch -p1 <../imlib2-bin-tinyxlib.patch
  patch -p1 <../imlib2-giflib-5.1.0.patch
}

build() {
  cd "imlib2-$pkgver"
  ./configure --prefix=/ --with-jpeg --with-png --with-gif --without-tiff \
    --with-zlib --with-bzip2 --without-id3 --disable-static
  make
}

package_imlib2() {
  cd "imlib2-$pkgver"
  make DESTDIR=$pkgdir install
  rm -rf "$pkgdir"/share
  cd "$pkgdir"/bin
  rm -rf *_*
}

package_imlib2-tools() {
  options=('strip')
  pkgdesc="imlib2 tools"
  depends=("imlib2" "tinyxlib")

  cd "imlib2-$pkgver"
  make DESTDIR=$pkgdir install
  rm -rf "$pkgdir"/include "$pkgdir"/lib
  cd "$pkgdir"/bin
  rm -rf *-*
}
