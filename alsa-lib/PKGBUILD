# big thanks to sabotage linux devs for the patches

pkgname=alsa-lib
pkgver=1.0.27.2
pkgrel=1
pkgdesc="ALSA sound library"
arch=('i686' 'x86_64')
url="http://alsa-project.org"
license=('GPL')
options=('strip' 'debug' 'libtool')
source=("ftp://ftp.alsa-project.org/pub/lib/$pkgname-$pkgver.tar.bz2"
	"alsa-lib_pcm_h.patch"
	"alsa-lib_mixed_types.patch"
	"alsa-lib_mips_atomic.patch"
	"alsa-lib_mask_inline.patch"
	"alsa-lib_kernel_off_t.patch"
	"alsa-lib-portable-mutex.patch")
md5sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')

build() {
  cd "$pkgname-$pkgver"
  rm -f src/compat/hsearch_r.c
  touch src/compat/hsearch_r.c

  patch -p1 < "$srcdir"/alsa-lib_pcm_h.patch
  patch -p1 < "$srcdir"/alsa-lib_mixed_types.patch
  patch -p1 < "$srcdir"/alsa-lib_mips_atomic.patch
  patch -p1 < "$srcdir"/alsa-lib_mask_inline.patch
  patch -p1 < "$srcdir"/alsa-lib_kernel_off_t.patch
  patch -p1 < "$srcdir"/alsa-lib-portable-mutex.patch

  # --without-versioned disables a 10-year-old ALSA API

  CFLAGS="$CFLAGS -include stdlib.h -D_POSIX_C_SOURCE=200809L -D_GNU_SOURCE" ./configure \
    --prefix=/ --disable-python --without-versioned

  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
