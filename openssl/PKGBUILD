pkgname=("openssl" "openssl-doc")
pkgbase=openssl
pkgver=1.0.1h
pkgrel=1
pkgdesc='OpenSSL crypto library'
arch=('i686' 'x86_64')
groups=("base")
url='http://www.openssl.org/'
license=('custom')
depends=("zlib")
options=('strip' 'debug')
source=("http://www.openssl.org/source/$pkgname-$pkgver.tar.gz")
md5sums=('SKIP')

prepare() {
	cd ${srcdir}/${pkgbase}-${pkgver}
	sed -i "s/-DTERMIO/-DTERMIOS/g" Configure
	sed -i "s/defined(linux)/0/" crypto/ui/ui_openssl.c
}

build() {
	cd ${srcdir}
	mv ${pkgbase}-${pkgver} ${pkgbase}
	cp -Rv ${pkgbase} ${pkgbase}-shared
	cd ${pkgbase}
	./config --prefix=/ --openssldir=/etc/ssl --libdir=lib zlib no-shared "-Wa,--noexecstack -D_GNU_SOURCE"
	make -j1
	cd ../${pkgbase}-shared
	./config --prefix=/ --openssldir=/etc/ssl --libdir=lib zlib shared "-Wa,--noexecstack -D_GNU_SOURCE -fPIC"
	make -j1 build_libs
	make -j1 build-shared
}

package_openssl() {
	cd ${srcdir}/${pkgbase}
	make -j1 INSTALL_PREFIX=${pkgdir} MANDIR=/share/man install
	rm -rf ${pkgdir}/share/man
	cd ../${pkgbase}-shared
	cp -f *.so* ${pkgdir}/lib
	cd ${pkgdir}/lib
	# Fix symlinks
	rm libcrypto.so libssl.so
	ln -s libcrypto.so.1.0.0 libcrypto.so
	ln -s libssl.so.1.0.0 libssl.so
}

package_openssl-doc() {
	cd ${srcdir}/${pkgbase}
	make -j1 INSTALL_PREFIX=${pkgdir} MANDIR=/share/man install
	cd ${pkgdir}
	rm -r ${pkgdir}/bin ${pkgdir}/etc ${pkgdir}/include ${pkgdir}/lib
}
