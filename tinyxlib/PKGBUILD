pkgname=("tinyxlib")
pkgver=0.0.1
pkgrel=1
pkgdesc="tiny X libraries implementation"
arch=('i686' 'x86_64')
url="https://github.com/idunham/tinyxlib"
source=("http://yuyu.asie.pl/tinyxlib-fee6aac.tar.xz"
        "Xcms.h"
	"tinyxlib-fix-makefile.diff"
        "tinyxlib-musl.patch")
md5sums=('SKIP' 'SKIP' 'SKIP' 'SKIP')
options=('strip' 'debug')

prepare() {
  cd "$pkgname"

  patch -p0 <../tinyxlib-fix-makefile.diff
  patch -p1 <../tinyxlib-musl.patch

  # Patch definitions
  sed -i -e "s@/usr/@/@g" -e "s@-pipe -Os -mtune=i386@-O2 -pipe -fPIC@g" standard_definitions.mk

  # Fix CMS build
  cp ../Xcms.h include/X11/

  # Patch paths
  for i in standard_definitions.mk libXt/IntrinsicI.h libXt/Makefile libXmu/Makefile libtinyX11/Makefile \
    libtinyX11/StrKeysym.c libtinyX11/ErrDes.c libtinyX11/cmsColNm.c libXcursor/library.c \
    libxtrans/Xtranssock.c; do
      echo "Fixing /usr in "$i"..."
      sed -i "s@/usr@/@g" "$i"
  done
}

build() {
  cd "$pkgname"
  make clean
  make
}

package() {
  cd "$pkgname"
  mkdir -p "$pkgdir"/share/X11
  mkdir -p "$pkgdir"/lib/pkgconfig
  mkdir -p "$pkgdir"/include
  make DESTDIR=$pkgdir install
}
