pkgname=xz
pkgver=5.0.5
pkgrel=3
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=('i686' 'x86_64')
groups=("base")
url='http://tukaani.org/xz/'
license=('GPL' 'LGPL' 'custom')
depends=('musl')
source=("http://tukaani.org/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "http://tukaani.org/${pkgname}/${pkgname}-${pkgver}.tar.gz.sig")
md5sums=('19d924e066b6fff0bc9d1981b4e53196'
         'SKIP')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	./configure --prefix=/ --disable-rpath --enable-werror --disable-static --enable-shared
	make
}

check() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make check
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
	install -d -m755 ${pkgdir}/share/licenses/xz/
	ln -sf /share/doc/xz/COPYING ${pkgdir}/share/licenses/xz/
}
