pkgname=("uuterm" "xuuterm")
pkgbase="uuterm"
pkgver=1
pkgrel=1
pkgdesc="lightweight framebuffer terminal"
arch=('i686' 'x86_64')
url="http://www.etalabs.net/uuterm.html"
license=('GPL')
source=("http://yuyu.asie.pl/uuterm-2008-02-10.tar.xz"
        "uuterm-32bit-blit.diff"
        "uuterm-palette-defpaths.diff"
        "palette")
md5sums=('SKIP' 'SKIP' 'SKIP' 'SKIP')
makedepends=("ncurses" "tinyxlib")

prepare() {
  cd "$pkgbase"
  patch -p1 < ../uuterm-32bit-blit.diff
  patch -p1 < ../uuterm-palette-defpaths.diff
}

build() {
  cd "$pkgbase"
  make uuterm-fb uuterm-x11 ucfcomp ytty/ytty.ucf
}

package_xuuterm() {
  depends=("tinyxlib" "uuterm")
  pkgdesc="lightweight X11 terminal"
  cd "$pkgbase"
  mkdir -p "$pkgdir"/bin
  install -c -o root -m 0755 uuterm-x11 "$pkgdir"/bin/xuuterm
}

package_uuterm() {
  cd "$pkgbase"
  mkdir -p "$pkgdir"/bin
  mkdir -p "$pkgdir"/share/fonts/ucf
  mkdir -p "$pkgdir"/share/uuterm
  mkdir -p "$pkgdir"/share/terminfo/u
  install -c -o root -m 0755 uuterm-fb "$pkgdir"/bin/uuterm
  install -c -o root -m 0755 ucfcomp "$pkgdir"/bin/ucfcomp
  install -c -o root -m 0644 ytty/ytty.ucf "$pkgdir"/share/fonts/ucf/ytty.ucf
  install -c -o root -m 0644 ../palette "$pkgdir"/share/uuterm/palette
  tic -o "$pkgdir"/share/terminfo uuterm.ti
  tic -o "$pkgdir"/share/terminfo uuterm-256color.ti
}
