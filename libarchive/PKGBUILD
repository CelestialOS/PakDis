pkgname=libarchive
pkgver=3.1.2
pkgrel=2
pkgdesc="library that can create and read several streaming archive formats"
arch=('i686' 'x86_64')
groups=("base")
url="http://libarchive.org/"
license=('BSD')
depends=('bzip2' 'xz' 'zlib')
options=('strip' 'debug' 'libtool')
source=("http://libarchive.org/downloads/$pkgname-$pkgver.tar.gz")
md5sums=('efad5a503f66329bb9d2f4308b5de98a')

prepare() {
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/ --with-zlib --with-bz2lib --without-lzmadec --without-iconv \
	--with-lzma --without-lzo2 --without-nettle --without-openssl \
	--without-xml2 --without-expat --disable-xattr --disable-acl \
	--enable-largefile --disable-static
  sed -i "s,#define HAVE_LCHMOD 1,,g" config.h
  make
}

check() {
  cd "$pkgname-$pkgver"

  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  ln -s /bin/bsdtar "$pkgdir"/bin/tar
  ln -s /bin/bsdcpio "$pkgdir"/bin/cpio

  install -D -m644 COPYING "$pkgdir"/share/licenses/libarchive/COPYING
}
