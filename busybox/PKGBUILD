pkgname=busybox
pkgver=1.22.1
pkgrel=4
pkgdesc="busybox lightweight shell replacement"
arch=('i686' 'x86_64')
groups=("base")
url="http://busybox.net"
license=('GPL')
makedepends=("linux-libre-headers")
backup=("etc/mdev.conf" "etc/udhcpc.rc")
source=("http://busybox.net/downloads/$pkgname-$pkgver.tar.bz2"
	"busybox.config"
	"busybox-blowfish.patch"
	"busybox-man-mandoc.patch"
	"busybox-musl-fixes.patch"
        "busybox-1.22.1-date.patch"
        "busybox-1.22.1-iplink.patch"
        "busybox-1.22.1-nc.patch"
	"gettext"
        "mdev.conf")
depends=("musl>=1.1.3")

# Gettext is technically not part of busybox, but it fits its spirit so I keep it here
# for now.

md5sums=('SKIP' 'SKIP' "SKIP" "SKIP" "SKIP" "SKIP" 'SKIP' "SKIP" "SKIP" "SKIP")

prepare() {
  cd "$pkgname-$pkgver"

  # Fix musl compatibility.
  patch -p1 < ../busybox-musl-fixes.patch

  # Allow usage of Blowfish encryption for password (Sabotage Linux)
  patch -p1 < ../busybox-blowfish.patch

  # Apply man changes:
  # - use mandoc instead of nroff
  # - use /share/man and less instead of more
  patch -p1 < ../busybox-man-mandoc.patch
  sed -i -e "s/\/usr\/man/\/share\/man/g" -e "s/\"more\"/\"less\"/" \
    miscutils/man.c

  # Busybox.net hotfixes
  patch -p1 < ../busybox-1.22.1-date.patch
  patch -p1 < ../busybox-1.22.1-iplink.patch
  patch -p1 < ../busybox-1.22.1-nc.patch
}

build() {
  cd "$pkgname-$pkgver"
  make allnoconfig KCONFIG_ALLCONFIG=../busybox.config
  make
}

package() {
  cd "$pkgname-$pkgver"
  make busybox.links

  mkdir -p "$pkgdir"/bin
  mkdir -p "$pkgdir"/etc

  cp busybox "$pkgdir"/bin/busybox

  cat busybox.links | while read line; do
    ln -sf /bin/busybox "$pkgdir"/bin/"$(basename $line)"
  done

  cp examples/udhcp/simple.script "$pkgdir"/etc/udhcpc.rc

  cd ..

  cp gettext "$pkgdir"/bin
  chmod +x "$pkgdir"/bin/gettext

  cp mdev.conf "$pkgdir"/etc
  mkdir -p "$pkgdir"/etc/mdev
  touch "$pkgdir"/etc/mdev/detect-drive
  chmod +x "$pkgdir"/etc/mdev/detect-drive
}
