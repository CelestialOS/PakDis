pkgname=busybox
pkgver=1.22.1
pkgrel=1
pkgdesc="busybox lightweight shell replacement"
arch=('i686' 'x86_64')
groups=("base")
url="http://busybox.net"
license=('GPL')
makedepends=("linux-libre-headers")
backup=("etc/mdev.conf" "etc/udhcpc.rc")
source=("http://busybox.net/downloads/$pkgname-$pkgver.tar.bz2"
	"busybox.config"
	"busybox-man-mandoc.patch"
	"busybox-musl-fixes.patch"
        "busybox-sed.patch"
	"gettext"
        "mdev.conf")

# Gettext is technically not part of busybox, but it fits its spirit so I keep it here.

md5sums=('SKIP' 'SKIP' "SKIP" "SKIP" "SKIP" "SKIP" 'SKIP')

prepare() {
  cd "$pkgname-$pkgver"

  # Fix musl compatibility.
  patch -p1 < ../busybox-musl-fixes.patch

  # Fix sed using GNU getopt extensions (removes -iSFX, though, so not a perfect solution)
  patch -p1 < ../busybox-sed.patch

  # Make busybox's man use mandoc in place of nroff
  patch -p1 < ../busybox-man-mandoc.patch

  # Change the defualt man directory
  sed -i "s/\/usr\/man/\/share\/man/g" miscutils/man.c
}

build() {
  cd "$pkgname-$pkgver"
  make allnoconfig KCONFIG_ALLCONFIG=../busybox.config
  make
}

package() {
  cd "$pkgname-$pkgver"
  make busybox.links

  mkdir -p "$pkgdir"/bin
  mkdir -p "$pkgdir"/etc

  cp busybox "$pkgdir"/bin/busybox

  cat busybox.links | while read line; do
    ln -sf /bin/busybox "$pkgdir"/bin/"$(basename $line)"
  done

  cp examples/udhcp/simple.script "$pkgdir"/etc/udhcpc.rc

  cd ..

  cp gettext "$pkgdir"/bin
  chmod +x "$pkgdir"/bin/gettext

  cp mdev.conf "$pkgdir"/etc
}
